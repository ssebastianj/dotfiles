#### =============== Theme "ssebastianj" for Prezto + Zsh ====================
# Based on "wikimatze" theme: https://git.io/vKtSn

function prompt_ssebastianj_precmd {
  git-info
}

function prompt_ssebastianj_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_ssebastianj_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_mseri_colors=(
      "%F{81}"  # Turquoise
      "%F{75}"  # Pastel Cyan
      "%F{215}" # Light Orange
      "%F{166}" # Orange - not used
      "%F{119}" # Pastel Limegreen
      "%F{255}" # White  
    )
  else
    _prompt_mseri_colors=(
      "%F{cyan}"
      "%F{magenta}"
      "%F{yellow}"
      "%F{red}"
      "%F{green}"
      "%F{white}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format '%%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%%B%F{white}%b%f%%b'
  zstyle ':prezto:module:git:info:stashed' format '%%B%F{cyan}✭%f%%b'

  zstyle ':prezto:module:git:info:clean' format '%F{green}✔%f'
  zstyle ':prezto:module:git:info:dirty' format '%F{red}✗%f'
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{white}◼%f%%b'

  zstyle ':prezto:module:git:info:keys' format \
      'prompt' ':%%B%F{white}(%f%%b$(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s%%B%F{white})%f%%b' \
      'rprompt' '%A%B%S%C%D%U%u'

  # Define prompts.
  PROMPT="%B${_prompt_mseri_colors[1]}%n%f%b@%B${_prompt_mseri_colors[2]}%m%f%b:%B${_prompt_mseri_colors[5]}%~%f%b"'${(e)git_info[prompt]}'"${_prompt_mseri_colors[6]}»%f "
  RPROMPT=''

}

prompt_ssebastianj_setup "$@"
